# # Sea ice advected by an atmospheric anticyclone
# 
#
#
#
using ClimaSeaIce
using ClimaSeaIce.SeaIceMomentumEquations
using ClimaSeaIce.Rheologies
using Oceananigans
using Oceananigans.Units
using Oceananigans.Operators
using Oceananigans.BoundaryConditions
using Printf
using CairoMakie

# The experiment found in the paper: 
# Simulating Linear Kinematic Features in Viscous-Plastic Sea Ice Models 
# on Quadrilateral and Triangular Grids With Different Variable Staggering

arch = CPU()

L  = 512kilometers
ùìã‚Çí = 0.01 # m / s maximum ocean speed
ùìã‚Çê = 30.0 # m / s maximum atmospheric speed modifier

# 2 km domain
grid = RectilinearGrid(arch;
                       size = (256, 256), 
                          x = (0, L), 
                          y = (0, L), 
                       halo = (7, 7),
                   topology = (Bounded, Bounded, Flat))

#####                   
##### Value boundary conditions for velocities
#####

u_bcs = FieldBoundaryConditions(north=ValueBoundaryCondition(0),
                                south=ValueBoundaryCondition(0))

v_bcs = FieldBoundaryConditions(west=ValueBoundaryCondition(0),
                                east=ValueBoundaryCondition(0))

#####
##### Ocean sea-ice stress
#####

# Constant ocean velocities corresponding to a cyclonic eddy
U‚Çí = Field{Face, Face, Nothing}(grid)
V‚Çí = Field{Face, Face, Nothing}(grid)

set!(U‚Çí, (x, y) -> ùìã‚Çí * (2y - L) / L)
set!(V‚Çí, (x, y) -> ùìã‚Çí * (L - 2x) / L)
fill_halo_regions!((U‚Çí, V‚Çí))

œÑ‚Çí = SemiImplicitStress(u‚Çë=U‚Çí, v‚Çë=V‚Çí)

####
#### Atmosphere - sea ice stress 
####

U‚Çê = Field{Face, Face, Nothing}(grid)
V‚Çê = Field{Face, Face, Nothing}(grid)

œÑ‚Çê = SemiImplicitStress(; u‚Çë=U‚Çê, v‚Çë=V‚Çê, œÅ‚Çë=1.3, C·¥∞=1.2e-3)

# Atmospheric velocities corresponding to an anticyclonic eddy moving north-east
@inline center(t) = 256kilometers + 51.2kilometers * t / 86400
@inline radius(x, y, t)  = sqrt((x - center(t))^2 + (y - center(t))^2)
@inline speed(x, y, t)   = 1 / 100 * exp(- radius(x, y, t) / 100kilometers)

@inline ua_time(x, y, t) = - ùìã‚Çê * speed(x, y, t) * (cosd(72) * (x - center(t)) + sind(72) * (y - center(t))) / 1000
@inline va_time(x, y, t) = - ùìã‚Çê * speed(x, y, t) * (cosd(72) * (y - center(t)) - sind(72) * (x - center(t))) / 1000

# Initialize the stress at time t = 0
set!(U‚Çê, (x, y) -> ua_time(x, y, 0))
set!(V‚Çê, (x, y) -> va_time(x, y, 0))

fill_halo_regions!((U‚Çê, V‚Çê))

#####
##### Numerical details
#####

# We use an elasto-visco-plastic rheology and WENO seventh order 
# for advection of h and ‚Ñµ

momentum_equations = SeaIceMomentumEquation(grid; 
                                            top_momentum_stress = œÑ‚Çê,
                                            bottom_momentum_stress = œÑ‚Çí,
                                            coriolis = FPlane(f=1e-4),
                                            ocean_velocities = (u = U‚Çí, v = V‚Çí),
                                            rheology = BrittleBinghamMaxwellRheology(),
                                            solver   = SplitExplicitSolver(substeps=150))

# Define the model!

model = SeaIceModel(grid; 
                    dynamics = momentum_equations,
                    ice_thermodynamics = nothing, # No thermodynamics here
                    advection = WENO(order=7),
                    tracers = :d,
                    boundary_conditions = (u=u_bcs, v=v_bcs))

# We start with a concentration of ‚Ñµ = 1 and an 
# initial height field with perturbations around 0.3 m

h‚ÇÄ(x, y) = 0.3 + 0.005 * (sin(60 * x / 1000kilometers) + sin(30 * y / 1000kilometers))

set!(model, h = h‚ÇÄ)
set!(model, ‚Ñµ = 1)

#####
##### Setup the simulation
#####

# run the model for 2 days

simulation = Simulation(model, Œît = 2minutes, stop_time = 2days)

# Remember to evolve the wind stress field in time!

function compute_wind_stress(sim)
    time = sim.model.clock.time
    @inline ua(x, y) = ua_time(x, y, time)
    @inline va(x, y) = va_time(x, y, time)
    set!(U‚Çê, ua)
    set!(V‚Çê, va)

    fill_halo_regions!((U‚Çê, V‚Çê))
    
    return nothing
end

simulation.callbacks[:top_stress] = Callback(compute_wind_stress, IterationInterval(1))

h = model.ice_thickness
‚Ñµ = model.ice_concentration
u, v = model.velocities

outputs = (; h, u, v, ‚Ñµ)

simulation.output_writers[:sea_ice] = JLD2OutputWriter(model, outputs;
                                                       filename = "sea_ice_advected_by_anticyclone.jld2", 
                                                       schedule = IterationInterval(5),
                                                       overwrite_existing = true)

wall_time = [time_ns()]

function progress(sim) 
    h = sim.model.ice_thickness
    ‚Ñµ = sim.model.ice_concentration
    u = sim.model.velocities.u
    v = sim.model.velocities.v

    hmax = maximum(interior(h))
    ‚Ñµmin = minimum(interior(‚Ñµ))
    umax = maximum(interior(u)), maximum(interior(v))
    step_time = 1e-9 * (time_ns() - wall_time[1])

    @info @sprintf("Time: %s, Iteration %d, Œît %s, max(vel): (%.2e, %.2e), max(h): %.2f, min(‚Ñµ): %.2f, wtime: %s \n",
                   prettytime(sim.model.clock.time),
                   sim.model.clock.iteration,
                   prettytime(sim.Œît),
                   umax..., hmax, ‚Ñµmin, prettytime(step_time))

     wall_time[1] = time_ns()
end

simulation.callbacks[:progress] = Callback(progress, IterationInterval(5))

run!(simulation)

using CairoMakie

htimeseries = FieldTimeSeries("sea_ice_advected_by_anticyclone.jld2", "h")
utimeseries = FieldTimeSeries("sea_ice_advected_by_anticyclone.jld2", "u")
vtimeseries = FieldTimeSeries("sea_ice_advected_by_anticyclone.jld2", "v")
‚Ñµtimeseries = FieldTimeSeries("sea_ice_advected_by_anticyclone.jld2", "‚Ñµ")

# Visualize!
Nt = length(htimeseries)
iter = Observable(1)

hi = @lift(htimeseries[$iter])
‚Ñµi = @lift(‚Ñµtimeseries[$iter])
ui = @lift(utimeseries[$iter])
vi = @lift(vtimeseries[$iter])

fig = Figure()
ax = Axis(fig[1, 1], title = "sea ice thickness")
heatmap!(ax, hi, colormap = :magma, colorrange = (0.23, 0.37))

ax = Axis(fig[1, 2], title = "sea ice concentration")
heatmap!(ax, ‚Ñµi, colormap = Reverse(:deep), colorrange = (0.9, 1))

ax = Axis(fig[2, 1], title = "zonal velocity")
heatmap!(ax, ui, colorrange = (-0.1, 0.1))

ax = Axis(fig[2, 2], title = "meridional velocity")
heatmap!(ax, vi, colorrange = (-0.1, 0.1))

CairoMakie.record(fig, "sea_ice_advected_by_anticyclone.mp4", 1:Nt, framerate = 8) do i
    iter[] = i
    @info "doing iter $i"
end
nothing #hide

# ![](sea_ice_advected_by_anticyclone.mp4)
