# # Sea ice advected by an atmospheric anticyclone example
#
# This example simulates sea ice advected by an atmospheric anticyclone, based on
# the experiment found in the paper:
#
# > Simulating Linear Kinematic Features in Viscous-Plastic Sea Ice Models on
# > Quadrilateral and Triangular Grids With Different Variable Staggering
#
# The ice is driven by a moving anticyclonic atmospheric eddy and interacts with
# a cyclonic ocean eddy. This example demonstrates how to:
#
#   * set up a two-dimensional sea ice model with time-varying atmospheric forcing,
#   * prescribe moving atmospheric and ocean eddies,
#   * use elasto-visco-plastic rheology with Coriolis effects,
#   * save and visualize time series data.
#
# ## Install dependencies
#
# First let's make sure we have all required packages installed.
#
# ```julia
# using Pkg
# pkg"add Oceananigans, ClimaSeaIce, CairoMakie"
# ```
#
# ## The physical domain
#
# We set up a two-dimensional bounded domain:

using ClimaSeaIce
using Oceananigans
using Oceananigans.Units
using Oceananigans.Operators
using Oceananigans.BoundaryConditions
using Printf
using CairoMakie

arch = CPU()

L  = 512kilometers
ùìã‚Çí = 0.01 # m s‚Åª¬π maximum ocean speed
ùìã‚Çê = 30.0 # m s‚Åª¬π maximum atmospheric speed modifier

grid = RectilinearGrid(arch;
                       size = (128, 128),
                          x = (0, L),
                          y = (0, L),
                       halo = (7, 7),
                   topology = (Bounded, Bounded, Flat))

# ## Boundary conditions
#
# We set value boundary conditions for velocities:

u_bcs = FieldBoundaryConditions(north=ValueBoundaryCondition(0),
                                south=ValueBoundaryCondition(0))

v_bcs = FieldBoundaryConditions(west=ValueBoundaryCondition(0),
                                east=ValueBoundaryCondition(0))

# ## Ocean sea-ice stress
#
# We set up constant ocean velocities corresponding to a cyclonic eddy:

U‚Çí = XFaceField(grid)
V‚Çí = YFaceField(grid)

set!(U‚Çí, (x, y) -> ùìã‚Çí * (2y - L) / L)
set!(V‚Çí, (x, y) -> ùìã‚Çí * (L - 2x) / L)
fill_halo_regions!((U‚Çí, V‚Çí))

œÑ‚Çí = SemiImplicitStress(u‚Çë=U‚Çí, v‚Çë=V‚Çí)

# ## Atmosphere-sea ice stress
#
# We set up atmospheric velocities corresponding to an anticyclonic eddy moving
# northeast. The eddy center moves with time:

@inline center(t) = 256kilometers + 51.2kilometers * t / day
@inline radius(x, y, t) = sqrt((x - center(t))^2 + (y - center(t))^2)
@inline speed(x, y, t) = 1 / 100 * exp(- radius(x, y, t) / 100kilometers)

@inline ua_time(x, y, t) = - ùìã‚Çê * speed(x, y, t) * (  cosd(72) * (x - center(t)) + sind(72) * (y - center(t))) / 1000
@inline va_time(x, y, t) = - ùìã‚Çê * speed(x, y, t) * (- sind(72) * (x - center(t)) + cosd(72) * (y - center(t))) / 1000

# Initialize the stress at time t = 0:

U‚Çê = XFaceField(grid)
V‚Çê = YFaceField(grid)

set!(U‚Çê, (x, y) -> ua_time(x, y, 0))
set!(V‚Çê, (x, y) -> va_time(x, y, 0))

fill_halo_regions!((U‚Çê, V‚Çê))

œÑ‚Çêu = Field(- U‚Çê * sqrt(U‚Çê^2 + V‚Çê^2) * 1.3 * 1.2e-3)
œÑ‚Çêv = Field(- V‚Çê * sqrt(U‚Çê^2 + V‚Çê^2) * 1.3 * 1.2e-3)

# ## Model configuration
#
# We use an elasto-visco-plastic rheology and WENO seventh order for advection
# of ice thickness and concentration. We include Coriolis effects:

dynamics = SeaIceMomentumEquation(grid;
                                  top_momentum_stress = (u=œÑ‚Çêu, v=œÑ‚Çêv),
                                  bottom_momentum_stress = œÑ‚Çí,
                                  coriolis = FPlane(f=1e-4))

model = SeaIceModel(grid;
                    dynamics,
                    ice_thermodynamics = nothing, # No thermodynamics here
                    advection = WENO(order=7),
                    boundary_conditions = (u=u_bcs, v=v_bcs))

# ## Initial conditions
#
# We start with a concentration of `‚Ñµ = 1` and an initial height field with
# perturbations around 0.3 meters:

h‚ÇÄ(x, y) = 0.3 + 0.005 * (sin(60 * x / 1000kilometers) + sin(30 * y / 1000kilometers))

set!(model, h = h‚ÇÄ)
set!(model, ‚Ñµ = 1)

# ## Running the simulation
#
# We run the model for 2 days with a 2-minute time step:

simulation = Simulation(model, Œît = 2minutes, stop_time = 2days)

# ## Time-varying wind stress
#
# We need to evolve the wind stress field in time. We set up a callback to
# update the atmospheric stress at each time step:

function compute_wind_stress(sim)
    time = sim.model.clock.time
    @inline ua(x, y) = ua_time(x, y, time)
    @inline va(x, y) = va_time(x, y, time)
    set!(U‚Çê, ua)
    set!(V‚Çê, va)

    fill_halo_regions!((U‚Çê, V‚Çê))

    compute!(œÑ‚Çêu)
    compute!(œÑ‚Çêv)

    return nothing
end

simulation.callbacks[:top_stress] = Callback(compute_wind_stress, IterationInterval(1))

# ## Output writer
#
# We set up an output writer to save the ice thickness, concentration, and
# velocity fields:

h = model.ice_thickness
‚Ñµ = model.ice_concentration
u, v = model.velocities

outputs = (; h, u, v, ‚Ñµ)

simulation.output_writers[:sea_ice] = JLD2Writer(model, outputs;
                                                 filename = "sea_ice_advected_by_anticyclone.jld2",
                                                 schedule = IterationInterval(5),
                                                 overwrite_existing = true)


# We can finally run the simulation

run!(simulation)

# ## Visualizing the results
#
# We load the saved time series and create an animation:

htimeseries = FieldTimeSeries("sea_ice_advected_by_anticyclone.jld2", "h")
utimeseries = FieldTimeSeries("sea_ice_advected_by_anticyclone.jld2", "u")
vtimeseries = FieldTimeSeries("sea_ice_advected_by_anticyclone.jld2", "v")
‚Ñµtimeseries = FieldTimeSeries("sea_ice_advected_by_anticyclone.jld2", "‚Ñµ")

Nt = length(htimeseries)
iter = Observable(1)

hi = @lift(interior(htimeseries[$iter], :, :, 1))
‚Ñµi = @lift(interior(‚Ñµtimeseries[$iter], :, :, 1))
ui = @lift(interior(utimeseries[$iter], :, :, 1))
vi = @lift(interior(vtimeseries[$iter], :, :, 1))

fig = Figure(size = (1200, 1200))
ax1 = Axis(fig[1, 1], title = "Sea ice thickness (m)")
ax2 = Axis(fig[1, 2], title = "Sea ice concentration")
ax3 = Axis(fig[2, 1], title = "Zonal velocity (m s‚Åª¬π)")
ax4 = Axis(fig[2, 2], title = "Meridional velocity (m s‚Åª¬π)")

heatmap!(ax1, hi, colormap = :magma, colorrange = (0.23, 0.37))
heatmap!(ax2, ‚Ñµi, colormap = Reverse(:deep), colorrange = (0.9, 1))
heatmap!(ax3, ui, colorrange = (-0.1, 0.1), colormap = :balance)
heatmap!(ax4, vi, colorrange = (-0.1, 0.1), colormap = :balance)

CairoMakie.record(fig, "sea_ice_advected_by_anticyclone.mp4", 1:Nt, framerate = 8) do i
    iter[] = i
    @info "Rendering frame $i"
end
nothing #hide

# ![](sea_ice_advected_by_anticyclone.mp4)
#
# The animation shows how the ice is advected and deformed by the moving
# anticyclonic atmospheric eddy. Linear kinematic features (leads and ridges)
# form as the ice responds to the complex stress patterns.
