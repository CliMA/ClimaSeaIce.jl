# # Sea ice advected by an atmospheric anticyclone
#
#
#
#

using Oceananigans
using Oceananigans.Units
using ClimaSeaIce
using Printf
using ClimaSeaIce.SeaIceDynamics

# The experiment found in the paper: 
# Simulating Linear Kinematic Features in Viscous-Plastic Sea Ice Models 
# on Quadrilateral and Triangular Grids With Different Variable Staggering

arch = CPU()

L  = 512kilometers
ùìã‚Çí = 0.01 # m / s maximum ocean speed
ùìã‚Çê = 30.0 # m / s maximum atmospheric speed modifier
C·¥∞ = 1.2e-3 # Atmosphere - sea ice drag coefficient
œÅ‚Çê = 1.3 # kg/m¬≥

# 2 km domain
grid = RectilinearGrid(arch;
                       size = (256, 256), 
                          x = (0, L), 
                          y = (0, L), 
                   topology = (Bounded, Bounded, Flat))

#####
##### Setup atmospheric and oceanic forcing
#####

# Constant ocean velocities corresponding to a cyclonic eddy
U‚Çí = XFaceField(grid)
V‚Çí = YFaceField(grid)

set!(U‚Çí, (x, y) -> ùìã‚Çí * (2y - L) / L)
set!(V‚Çí, (x, y) -> ùìã‚Çí * (L - 2x) / L)

U‚Çê = XFaceField(grid)
V‚Çê = YFaceField(grid)

# Atmosphere - sea ice stress
œÑ·µ§ = Field(œÅ‚Çê * C·¥∞ * sqrt(U‚Çê^2 + V‚Çê^2) * U‚Çê)
œÑ·µ• = Field(œÅ‚Çê * C·¥∞ * sqrt(U‚Çê^2 + V‚Çê^2) * V‚Çê)

# Atmospheric velocities corresponding to an anticyclonic eddy moving north-east
@inline center(t) = 256kilometers + 51.2kilometers * t / 86400
@inline radius(x, y, t)  = sqrt((x - center(t))^2 + (y - center(t))^2)
@inline speed(x, y, t)   = 1 / 100 * exp(- radius(x, y, t) / 100kilometers)

@inline ua_time(x, y, t) = - ùìã‚Çê * speed(x, y, t) * (cosd(72) * (x - center(t)) + sind(72) * (y - center(t))) / 1000
@inline va_time(x, y, t) = - ùìã‚Çê * speed(x, y, t) * (cosd(72) * (y - center(t)) - sind(72) * (x - center(t))) / 1000

# Initialize the stress at time t = 0
set!(U‚Çê, (x, y) -> ua_time(x, y, 0))
set!(V‚Çê, (x, y) -> va_time(x, y, 0))
compute!(œÑ·µ§)
compute!(œÑ·µ•)

#####
##### Numerical details
#####

# We use an elasto-visco-plastic rheology and WENO seventh order 
# for advection of h and ‚Ñµ
momentum_solver = ExplicitDynamics(grid)
advection = WENO(; order = 7)

u_bcs = FieldBoundaryConditions(top = nothing, bottom = nothing,
                                north = ValueBoundaryCondition(0),
                                south = ValueBoundaryCondition(0))

v_bcs = FieldBoundaryConditions(top = nothing, bottom = nothing,
                                west = ValueBoundaryCondition(0),
                                east = ValueBoundaryCondition(0))

# Define the model!
model = SeaIceModel(grid; 
                    top_u_stress = œÑ·µ§,
                    top_v_stress = œÑ·µ•,
                    ocean_velocities = (u = U‚Çí, v = V‚Çí),
                    ice_dynamics = momentum_solver,
                    advection,
                    boundary_conditions = (u = u_bcs, v = v_bcs),
                    coriolis = FPlane(f = 1e-4))

# Initial height field with perturbations around 0.3 m
h‚ÇÄ(x, y) = 0.3 + 0.005 * (sin(60 * x / 1000kilometers) + sin(30 * y / 1000kilometers))

# We start with a concentration of ‚Ñµ = 1
set!(model, h = h‚ÇÄ)
set!(model, ‚Ñµ = 1)

#####
##### Setup the simulation
#####

# run the model for 2 days
simulation = Simulation(model, Œît = 2minutes, stop_time = 2days)

# Remember to evolve the wind stress field in time!
function compute_wind_stress(sim)
    time = sim.model.clock.time
    @inline ua(x, y) = ua_time(x, y, time)
    @inline va(x, y) = va_time(x, y, time)
    set!(U‚Çê, ua)
    set!(V‚Çê, va)

    compute!(œÑ·µ§)
    compute!(œÑ·µ•)

    return nothing
end

simulation.callbacks[:top_stress] = Callback(compute_wind_stress, IterationInterval(1))

# Container to hold the data
htimeseries = []
‚Ñµtimeseries = []
utimeseries = []
vtimeseries = []

## Callback function to collect the data from the `sim`ulation
function accumulate_timeseries(sim)
    h = sim.model.ice_thickness
    ‚Ñµ = sim.model.ice_concentration
    u = sim.model.velocities.u
    v = sim.model.velocities.v
    push!(htimeseries, deepcopy(interior(h)))
    push!(‚Ñµtimeseries, deepcopy(interior(‚Ñµ)))
    push!(utimeseries, deepcopy(interior(u)))
    push!(vtimeseries, deepcopy(interior(v)))
end

wall_time = [time_ns()]

function progress(sim) 
    h = sim.model.ice_thickness
    ‚Ñµ = sim.model.ice_concentration
    u = sim.model.velocities.u
    v = sim.model.velocities.v

    hmax = maximum(interior(h))
    ‚Ñµmin = minimum(interior(‚Ñµ))
    umax = maximum(interior(u)), maximum(interior(v))
    step_time = 1e-9 * (time_ns() - wall_time[1])

    @info @sprintf("Time: %s, Iteration %d, Œît %s, max(vel): (%.2e, %.2e), max(h): %.2f, min(‚Ñµ): %.2f, wtime: %s \n",
                   prettytime(sim.model.clock.time),
                   sim.model.clock.iteration,
                   prettytime(sim.Œît),
                   umax..., hmax, ‚Ñµmin, prettytime(step_time))

     wall_time[1] = time_ns()
end

simulation.callbacks[:progress] = Callback(progress, IterationInterval(5))
simulation.callbacks[:save]     = Callback(accumulate_timeseries, IterationInterval(5))

run!(simulation)

using CairoMakie

# Visualize!
Nt = length(htimeseries)
iter = Observable(1)

hi = @lift(htimeseries[$iter][:, :, 1])
‚Ñµi = @lift(‚Ñµtimeseries[$iter][:, :, 1])
ui = @lift(utimeseries[$iter][:, :, 1])
vi = @lift(vtimeseries[$iter][:, :, 1])

fig = Figure()
ax = Axis(fig[1, 1], title = "sea ice thickness")
heatmap!(ax, hi, colormap = :magma,         colorrange = (0.23, 0.37))

ax = Axis(fig[1, 2], title = "sea ice concentration")
heatmap!(ax, ‚Ñµi, colormap = Reverse(:deep), colorrange = (0.75, 1))

ax = Axis(fig[2, 1], title = "zonal velocity")
heatmap!(ax, ui, colorrange = (-0.1, 0.1))

ax = Axis(fig[2, 2], title = "meridional velocity")
heatmap!(ax, vi, colorrange = (-0.1, 0.1))

CairoMakie.record(fig, "sea_ice_dynamics.mp4", 1:Nt, framerate = 8) do i
    iter[] = i
    @info "doing iter $i"
end
